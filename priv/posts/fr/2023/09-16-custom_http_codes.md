%{
  title: "Cr√©ez vos propres codes HTTP dans Phoenix avec Plug",
  author: "Simon Tirant",
  tags: ~w(elixir phoenix plug web http),
  lang: "french",
  description: "Marre des `404 not found` et autres `500 internal server error`? H√© bien, soyez cr√©atifs et inventez vos propres codes de status HTTP √† l'int√©rieur de votre application Phoenix avec Plug!",
  illustration: "/images/posts/error.gif",
  illustration_alt: "Message d'erreur anim√©"
}
---
Par d√©faut - √† l'int√©rieur de votre application [Phoenix](https://www.phoenixframework.org/) - la plupart des exceptions lev√©es sont des erreurs `500`: la tristement c√©l√®bre `Internal Server Error`, indiquant que le d√©veloppeur du site est un vilain programmeur qui a laiss√© des fautes dans son code et qui a oubli√© de se relire. Mais voil√†, parfois c'est aussi la faute du visiteur! Alors s'il vous pla√Æt, ne mordez pas la main qui vous nourrit avec du si bon contenu et naviguez en sachant o√π vous allez!

Donc, dans mon cas je voulais lever deux autres types d'exceptions que la `500`: la premi√®re √©tait la fameuse `404 Not Found`, mais j'ai aussi d√©cid√© de cr√©er une erreur personnalis√©e et plus sp√©cifique `461 No Blog Posts Found` . Voici mon histoire ... üìñ

![Vieux livre s'ouvrant](/images/posts/old_book.webp)

### 404 101 üßë‚Äçüéì

Pour la premi√®re d'entre elles, je voulais lever une exception quand un article sp√©cifique n'√©tait pas trouv√© pour une langue en particulier. Par exemple, je g√®re la traduction sur ce site avec le chemin de l'url; regardez la barre d'adresse: l'article que vous √™tes en train de lire actuellement a un nom de domaine `blog.simontirant.dev` / puis la route `post` / puis l'id de l'article (`custom_http_codes`) / enfin l'attribut de langue qui peut √™tre soit `french` soit `english`. Si vous d√©cidez - pour quelque raison que ce soit - de changer ce dernier attribut du chemin pour √©crire `silbo` √† la place (n.b.: le *silbo* est une antique langue siffl√©e utilis√©e √† Gomera, une des √Æles Canaries), alors le serveur l√®vera cette exception `404 Not Found` - √† la place de la `500` par d√©faut - car, bien que je puisse toujours apprendre le silbo en ligne et enregistrer mon article tech dans un fichier audio pour que vous puissiez l'√©couter dans un lecteur web, je ne suis pas s√ªr qu'il y ait un public pour √ßa.

Concr√®tement, ma fonction `show/2` situ√©e dans mon `BlogController` appelle une autre fonction, situ√©e dans le module `Blog`, nomm√©e `get_post_by_id_and_lang!/2`, en lui passant comme param√®tres les attributs d'url concernant la langue et l'id de l'article. Cette fonction `Blog.get_post_by_id_and_lang!/2` est ensuite charg√©e de retrouver l'article par son id et sa langue (car deux articles peuvent avoir le m√™me id s'ils sont dans une langue diff√©rente), si `nil` elle l√®ve une exception appel√©e `NotFoundError` qui a √©t√© auparavant d√©clar√©e dans son propre module. Voici un extrait de mon code ci-dessous: 

```elixir
defmodule NimbleBlog.Blog do

  @posts Enum.sort_by(@posts, & &1.date, {:desc, Date})

  def all_posts, do: @posts

  defmodule NotFoundError, do: defexception([:message, plug_status: 404])

  @doc """
  Returns a specific %Post{} from the combination of its id and language.
  Raise if nothing is found.

  #### Example:
      iex> get_post_by_id_and_lang!("post_1", "english")
      %Post{}

      iex> get_post_by_id_and_lang!("post_1", "german")
      ** (NotFoundError) post with both id="post_1" and lang="german" not found
  """
  @spec get_post_by_id_and_lang!(String.t(), String.t()) :: %Post{} | NotFoundError
  def get_post_by_id_and_lang!(id, lang) do
    Enum.find(all_posts(), &(&1.id == id && &1.lang == lang)) ||
      raise NotFoundError, "post with both id=#{id} and lang=#{lang} not found"
  end
end
```

*P.S.: ce blog utilise la librairie [NimblePublisher](https://hex.pm/packages/nimble_publisher) pour rendre des articles de blog √† partir de fichiers [Markdown](https://daringfireball.net/projects/markdown/) situ√©s dans un fichier du serveur. Comme je n'utilise pas de base de donn√©es, il n'y a donc pas de fonctions [Ecto](https://hexdocs.pm/ecto/Ecto.html) telles que `Repo.get!/3`, mais des fonctions venant du module `Enum` √† la place. [Lisez la premi√®re partie de mon article sur NimblePublisher ici](https://blog.simontirant.dev/post/nimble_blog_1/french).*

Comme nous pouvons le voir, l'exception d√©clar√©e comme `NotFoundError` est li√©e √† un [code HTTP](https://developer.mozilla.org/en-US/docs/Web/HTTP/Status) `404` classique, ceci gr√¢ce √† l'option `plug_status: 404` que j'ai ajout√© juste apr√®s `:message` dans la d√©claration de mon exception. La `404` est un [code HTTP](https://developer.mozilla.org/en-US/docs/Web/HTTP/Status) bien connu et [Plug](https://hexdocs.pm/plug/readme.html) la connait d√©j√†, regardez au tout d√©but du module `Plug.Conn.Status`, situ√© dans le dossier des `deps`:

```elixir
  statuses = %{
    ...,
    400 => "Bad Request",
    401 => "Unauthorized",
    402 => "Payment Required",
    403 => "Forbidden",
    404 => "Not Found",
    405 => "Method Not Allowed",
    406 => "Not Acceptable",
    407 => "Proxy Authentication Required",
    408 => "Request Timeout",
    409 => "Conflict",
    410 => "Gone",
    411 => "Length Required",
    412 => "Precondition Failed",
    413 => "Request Entity Too Large",
    414 => "Request-URI Too Long",
    415 => "Unsupported Media Type",
    416 => "Requested Range Not Satisfiable",
    417 => "Expectation Failed",
    418 => "I'm a teapot",
    421 => "Misdirected Request",
    422 => "Unprocessable Entity",
    423 => "Locked",
    424 => "Failed Dependency",
    425 => "Too Early",
    426 => "Upgrade Required",
    428 => "Precondition Required",
    429 => "Too Many Requests",
    431 => "Request Header Fields Too Large",
    451 => "Unavailable For Legal Reasons",
    ...
  }
```

Cela signifie que si ma fonction √©choue √† retrouver un article de blog pour cet id et cette langue - sous-entendant qu'au moins un des deux arguments de l'url, situ√©s apr√®s la route `post`, est faux - alors l' exception `NotFoundError` est lev√©e, appelant l'erreur `404` de [Plug](https://hexdocs.pm/plug/readme.html), puis [Phoenix](https://www.phoenixframework.org/) redirige le visiteur vers la page `404.html.heex` que j'ai cr√©√© √† l'int√©rieur de mon dossier `error_html`, lui-m√™me situ√© dans le dossier des `controllers`. Pour appeler ce _template_ en particulier, il faut se rendre dans la vue `error_html.ex` (toujours dans le dossiers des `controllers`) et ajouter `embed_templates("error_html/*")` - √† la place de la fonction `render/2` d√©j√† pr√©sente. Avec ceci, [Phoenix](https://www.phoenixframework.org/) pourra afficher les templates du dossier `error_html` pour vos pages d'erreur personnalis√©es ([lien vers la page d√©di√©e de la documentation](https://hexdocs.pm/phoenix/1.7.7/custom_error_pages.html)).

Ce m√©canisme consistant √† appeler le status `404` de [Plug](https://hexdocs.pm/plug/readme.html) depuis une exception, [Phoenix](https://www.phoenixframework.org/) le fait lui aussi pour une `NoRouteError` - une exception nativement lev√©e par le module `Phoenix.Router` quant une route simple n'existe tout simplement pas: par exemple, si vous √©crivez `/pist` au lieu de `/post` dans l'url. Dans notre cas, la langue et l'id √©tant des param√®tres et non pas une route, un terme incorrect sur ceux-ci aurait nativement appel√© l'erreur `500` et non pas `404`; mais j'aurais donc tout aussi bien pu d√©cider d'importer et de lever dans mon module `Blog`  l'exception native `Phoenix.Router.NotFoundError`, au lieu de cr√©er ma propre exception, car elle appelle aussi le status `404` de [Plug](https://hexdocs.pm/plug/readme.html). 

Mais si vous avez r√©ellement regard√©, comme je l'ai demand√©, en haut du module `Plug.Conn.Status` de n'importe quelle projet [Phoenix](https://www.phoenixframework.org/), vous devez avoir vu ceci juste avant la liste des status HTTP : 

```elixir
  custom_statuses = Application.compile_env(:plug, :statuses, %{})
```

Vous savez ce que √ßa veut dire ? √áa veut dire que, m√™me si la [liste des codes HTTP](https://developer.mozilla.org/en-US/docs/Web/HTTP/Status) a d√©j√† anticip√© des blagues √† l'intention des d√©veloppeurs - telles que `418 I'm a teapot` ou, ma pr√©f√©r√©e, `451 Unavailable For Legal Reasons` ... la censure d'√©tat dans _Fahrenheit 451_, vous l'avez ? - afin qu'ils puissient rigoler un peu, elle laisse tout un tas de codes disponibles afin de cr√©er vos propres status bizarres.

### Pr√™t √† jouer avec les nombres ? üßÆ

Donc, j'ai aussi besoin de lever une erreur quand le r√©sultat d'un filtre par tags dans une langue en particulier est vide. Ainsi, si vous cliquez, par exemple, sur le tag `tricot üß∂` puis sur le drapeau `anglais üá¨üáß` pour filtrer uniquement les articles en anglais sur le tricot, et bien vous allez lever mon [code HTTP]((https://developer.mozilla.org/en-US/docs/Web/HTTP/Status)) personnalis√© `461 No Blog Posts Found` ... Car je n'ai pas encore √©crit d'article en anglais sur le sujet. Et parce qu'il s'agit ici d'un blog tech, l'exception aurait √©t√© lev√©e en `fran√ßais üá´üá∑` aussi ! Ce status, comme le `404`, va ensuite appeler la vue `461.html.heex` situ√©e dans le dossier `error_html` pour vous expliquer que le serveur n'a trouv√© aucun article pour votre requ√™te. 

Mais ... Comment ai-je fait ceci ? Est-ce de la **magie noire** ? *Boooo...*üï∑Ô∏èüîÆü™Ñüßø

Et bien, en v√©rit√© non. Je l'ai juste fait en utilisant la fonction `Config.config/3` sur les status de [Plug](https://hexdocs.pm/plug/readme.html) √† l'int√©rieur du fichier `config.exs` dans le r√©pertoire `config`:

```elixir
import Config

config :plug, :statuses, %{461 => "No Blog Posts Found"}
```

De cette fa√ßon, lors de la compilation, [Plug](https://hexdocs.pm/plug/readme.html) va construitre sa map de `status` que nous avons vue plus t√¥t avec le [code HTTP](https://developer.mozilla.org/en-US/docs/Web/HTTP/Status) `404`, **mais en incluant cette fois-ci** les status personnalis√©s que nous avons pass√© dans le fichier `config.exs`. Comme vous l'aurez peut-√™tre compris, il est n√©cessaire de recompiler la d√©pendance [Plug](https://hexdocs.pm/plug/readme.html) apr√®s cette modification et, dans mon cas, j'ai d√ª utiliser `mix deps.compile plug --force`, sans quoi `mix` retournait une erreur si je n'ajoutais pas l'option `--force`. Apr√®s cela, je peux ensuite faire ce qui suit dans mon module `Blog` afin de filtrer les articles de blog:

```elixir 
defmodule NimbleBlog.Blog do
  @posts Enum.sort_by(@posts, & &1.date, {:desc, Date})

  def all_posts, do: @posts

  defmodule NoBlogPostsError, do: defexception([:message, plug_status: 461])

  @doc """
  Gets the list of posts filtered by a specific tag or language or a combination of both.

  Example:
      iex> list_posts_by_filters!(%{tag: "elixir", lang: "english"})
      [%Post{lang: "english", tags: ["elixir", ...]}, ...]

      iex> list_posts_by_filters!(%{tag: "cooking", lang: "sumerian"})
      ** (NoBlogPostsError) posts no found with tag cooking in sumerian language.
  """
  @spec list_posts_by_filters!(%{tag: String.t() | nil, lang: String.t() | nil}) ::
          [%Post{}] | NoBlogPostsError
  def list_posts_by_filters!(%{"tag" => "all", "lang" => "all"}), do: all_posts()
  def list_posts_by_filters!(%{"tag" => "all", "lang" => lang}), do: lang |> list_posts_by_lang!()
  def list_posts_by_filters!(%{"tag" => tag, "lang" => "all"}), do: tag |> list_posts_by_tag!()

  def list_posts_by_filters!(%{"tag" => tag, "lang" => lang}) do
    all_posts()
    |> Enum.filter(&(lang == &1.lang))
    |> Enum.filter(&(tag in &1.tags))
    |> case do
      [] -> raise NoBlogPostsError, "posts no found with tag #{tag} in #{lang} language."
      posts -> posts
    end
  end

  defp list_posts_by_tag!(tag) do
    case Enum.filter(all_posts(), &(tag in &1.tags)) do
      [] -> raise NoBlogPostsError, "posts with tag=#{tag} not found"
      posts -> posts
    end
  end

  defp list_posts_by_lang!(lang) do
    case Enum.filter(all_posts(), &(lang == &1.lang)) do
      [] -> raise NoBlogPostsError, "posts for #{lang} language not found"
      posts -> posts
    end
  end
end
```

Comme pour le code `404`, j'ai d√©clar√© mon exception `NoBlogPostsError` √† l'int√©rieur de son propre module et j'ai utilis√© cette fois-ci l'option `plug_status: 461` afin d'appeler le status personalis√© que nous avons param√©tr√© auparavant dans `config.exs`. Les fonctions en charge de retourner la liste des `%Post{}` - filtr√©e par langue, par tag ou les deux - peuvent ensuite lever le module d'exception `NoBlogPostsError` dans le cas o√π la liste des `%Post{}` serait vide.

*N.B.: cette m√©thode, pour ajouter des codes personnalis√©s √† partir de ceux qui ne sont pas utilis√©s par la [liste des codes HTTP](https://developer.mozilla.org/en-US/docs/Web/HTTP/Status) standard, peut aussi √™tre utilis√©e pour cr√©er un message de status diff√©rent pour un code existant d√©j√†. Ainsi, vous pouvez modifier le `404 Not Found` pour un effront√© `404 Found But Don't Want To Show It To You`; ou encore `451 Sorry But It Burned!`. Super!* 